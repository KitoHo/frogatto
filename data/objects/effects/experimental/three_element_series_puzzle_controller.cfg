{
id: "three_element_series_puzzle_controller",
ignore_collide: true,
activation_border: 350,
hidden_in_game: true,
zorder: "@include data/zorder.cfg:in_front_of_everything",
properties: {
	_valid_list: { type: "[custom_obj]", default: [] },
	_pushed_list: { type: "[custom_obj]", default: [] },

	_switch_1: { type: "string", default: '', editor_info: { type: "label" }},
	_switch_2: { type: "string", default: '', editor_info: { type: "label" }},
	_switch_3: { type: "string", default: '', editor_info: { type: "label" }},
	_linked_object: { type: "string", default: '', editor_info: { type: "label" }},
},

on_create: "if(_switch_1 and _switch_2 and _switch_3,
				[
					set(_valid_list, [get_object_or_die(level, _switch_1), get_object_or_die(level, _switch_2), get_object_or_die(level, _switch_3)]),
					fire_event('shuffle_list')
				]
			)",

on_shuffle_list: "[set(_valid_list, shuffle(_valid_list))]",

on_button_pressed: "[debug('button pushed by '+ (string <- arg.label)),if(arg in _valid_list, [set(_pushed_list, _pushed_list + [(custom_obj <- arg)]), fire_event('check_success')])]",
on_check_success: "if(size(_pushed_list) = size(_valid_list), 
						if(_valid_list = _pushed_list,
							fire_event('handle_success'),
							fire_event('handle_failure')
						)
					)",
on_handle_success: "if(_linked_object,
						[fire_event(get_object_or_die(level,_linked_object), 'button_pressed'),debug('success')]
					)",
on_handle_failure: "[debug('sequence failed'),set(_pushed_list, [])]",
on_end_anim: "animation('normal')",

editor_info: {
	category: "controllers",
},
animation: {
	id: "normal",
	image: "effects/particles.png",
	x: 86,
	y: 11,
	w: 28,
	h: 28,
	frames: 1,
	duration: 1000,
},
}