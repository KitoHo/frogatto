{
id: "padlock",
zorder: "@include data/zorder.cfg:player",
zsub_order: -10,

properties: {
	color: { type: "string", editor_info: { type: "enum", value:"'gold'", enum_values: ["gold", "silver", "green", "red", "purple", "blue", "cyan"] } },
	event_name: { type: "string", default: "", editor_info: { type: "text" } },

	set_to_animation: "def(string name) -> commands set(animation, { image: 'props/locks.png', duration: -1, id: name, rect: image_rect_for_color[color] })",
		
	image_rect_for_color: "{string->[int,int,int,int]} :: {
								'gold':			[0,0,15,15],
								'silver':		[16,0,31,15],
								'green':		[32,0,47,15],
								'red':			[48,0,63,15],
								'purple':		[64,0,79,15],
								'blue':			[80,0,95,15],
								'cyan':			[96,0,111,15]
							}",								

},
on_create: "bind_command(def() set_to_animation('normal'))",
on_load: "fire_event('create')",

on_start_level: "[ if(level.player.get_stored_event(event_name) = 1, remove_object(me)), animation('normal')]",
on_unlock: "[ level.player.store_event(event_name, 1),
				impact_cloud(mid_x,mid_y,'large') where impact_cloud = def(int new_x, int new_y, string size) -> commands [if(size = 'small', spawn('impact_cloud_small',new_x,new_y,1), spawn('impact_cloud',new_x,new_y,1)),sound('bump-2.wav')],
				map([0]*1d2, spawn('bouncing_debris_chunk',x+1d10, y+1d10, if(1d2=2,1,-1), [add(child.variations, ['metal_flat']),child.init_vel('burst')])),
				map([0]*1d2, spawn('bouncing_debris_chunk',x+1d10, y+1d10, if(1d2=2,1,-1), [add(child.variations, ['metal_round']),child.init_vel('burst')])),
				sound('BlockBreak.ogg'),die()]",
no_compile_image: "props/locks.png",
editor_info: {
	category: "keys & triggers",
	help: "Place this on top of a door to lock it - the door will autodetect the lock visually
		sitting there, and become locked.  To make the game remember that the act of unlocking 
		has happened, you have to set a matching event name in both the lock and the key - once
		this event gets triggered, they will cease being there when you enter the level.",
},

on_end_anim: "set_to_animation('normal')",

animation: {
	image: "props/locks.png",
	id: "normal",
	rect: [0,0,15,15],

	#gold is the default
	duration: 1,
},
}
